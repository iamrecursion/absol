\chapter{The ABSOL Testing File} % (fold)
\label{cha:the_absol_testing_file}
This appendix contains the test file used to check the functionality of the metacompiler both during and after its development. 
The file is contained in the following listing and defines a toy language that doesn't do anything useful.
Its main intention is to test the parsing functionality of \gls{absol}, as well as the semantic verification facilities. 

\begin{minted}[fontsize=\blockfont]{text}
// Language metadata
name : simple_test;

version : 0.0.1a;

using : {
    number,
    base,
    traverse
};

// Specification of the base-case semantics for this test language
truths : {
    {integer n <= <integer>},
    {floating n <= <floating>},
    {number n <= <number>}
};

language : {

// The start symbol
<<simple_test>> ::= <statement> | <a> ;

// Productions whose semantics should be inferred.
<statement> ::= <my-number> | <number> | <arith-expr> | <test> | <assignment> ;
<my-number> ::= <integer> | <floating> ;

// Should have inferred semantics, mutually recurses with <arith-op>.
<arith-expr> ::= <my-number> | <arith-op> ;

// Mutually recursive productions with infinite syntax.
// These should not impact on successful verification.
<a> ::= <b> ;
<b> ::= <a> ;

// A production that should not have semantics as it's never used semantically.
<empty> ::= ""; 

// Testing user-defined semantics with both kinds of alternations. 
// Testing guard verification.
<arith-op> ::= 
    <arith-expr> "+" <arith-expr> --> {
        number n : {n = n1 + n2}() :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]}
    } |
    <arith-expr> "-" <arith-expr> --> {
        number n : {n = n1 - n2}() :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]}
    } |
    <arith-expr> "*" <arith-expr> --> {
        number n : {n = n1 * n2}() :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]}
    } |
    <arith-expr> "/" <arith-expr> --> {
        number n : {n = n1 / n2}() :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]}
    } |
    <arith-expr> "^" <arith-expr> --> {
        number n : {n = 1}(n1 == 1) :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]} |
        number n : {n = n1 * n1}(n2 == 2) :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]} |
        number n : {n = n1 ^ n2}() :
            {number n1 <= <arith-expr>[0]}, {number n2 <= <arith-expr>[1]}
    };

// Test for parsing and checking of environment input rules. 
<assignment> ::= <statement> "=" <arith-expr> <empty> --> {
    none e <-- <statement>[0] : <arith-expr>[0]
};

// Test for parsing and checking of special syntax rules. 
<test> ::= <statement> "traverse" <statement> --> {
    any map(<statement>[0], <statement>[1])
};

<env-access> ::= <statement> --> {
    any e.<statement>[0]
};

};

\end{minted}

% chapter the_absol_testing_file (end)
